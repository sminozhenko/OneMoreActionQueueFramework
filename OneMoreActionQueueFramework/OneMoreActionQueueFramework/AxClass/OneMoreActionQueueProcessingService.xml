<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>OneMoreActionQueueProcessingService</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Action queue processing service class
/// </summary>
internal class OneMoreActionQueueProcessingService extends SysOperationServiceBase
{    
    private List            batchBundlingList;
    private BatchHeader     batchHeader;
    private int             batchBundleSize;
    private boolean         enableBatchBundling;
    private boolean         runBatchTaskInSequence;

    private SysOperationServiceController lastTask;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>processQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Process action queue from query
    /// </summary>
    /// <param name = "_contract"><c>OneMoreActionQueueProcessingQueryContract</c></param>
    [Hookable(false)]
    public void processQuery(OneMoreActionQueueProcessingQueryContract _contract)
    {
        QueryRun                            queryRun;
        boolean                             throwError;

        enableBatchBundling = _contract.parmEnableBatchBundling();
        batchBundleSize = _contract.parmBatchBundleSize();

        queryRun = new queryRun(_contract.getQuery());
    
        while(queryRun.next())
        {
            if (!this.executeAction(queryRun.get(tableNum(OneMoreActionQueue))))
            {
                throwError = true;
            }
        }

        this.finalizeExecution();

        //Batch job should fail if there were errors
        if (throwError)
        {
            throw Exception::Error;
        }

    }

]]></Source>
			</Method>
			<Method>
				<Name>addToQueue</Name>
				<Source><![CDATA[
    [Hookable(false)]
    private void addToQueue(OneMoreActionQueue _oneMoreActionQueue)
    {
        if (!batchBundlingList || batchBundlingList.elements() >= batchBundleSize)
        {
            if (batchBundlingList)
            {
                this.createTask(batchBundlingList.pack());
            }
                                        
            batchBundlingList = new List(Types::Int64);
        }
            
        batchBundlingList.addEnd(_oneMoreActionQueue.RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createTask</Name>
				<Source><![CDATA[
    [Hookable(false)]
    private void createTask(container _recIdListPacked)
    {
        SysOperationServiceController                       controller;
        OneMoreActionQueueProcessingBatchTaskBundleContract batchTaskBundleContract;
        
        if (this.isExecutingInBatch() && !batchHeader)
        {
            batchHeader = this.getCurrentBatchHeader();
        }
        
        controller = new SysOperationServiceController(classStr(OneMoreActionQueueProcessingService), methodStr(OneMoreActionQueueProcessingService, runBatchTaskBundle));

        batchTaskBundleContract = controller.getDataContractObject('_contract');
        batchTaskBundleContract.parmRecIdListPacked(_recIdListPacked);

        if (this.isExecutingInBatch())
        {
            batchHeader.addRuntimeTask(controller, this.getCurrentBatchTask().RecId);

            this.addTaskDependency(controller);
        }
        else
        {
            controller.run();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>processActionQueueRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Process single record from action queue
    /// </summary>
    /// <param name = "_contract"><c>OneMoreActionQueueProcessingSingleContract</c></param>
    [Hookable(false)]
    public void processActionQueueRecord(OneMoreActionQueueProcessingSingleContract _contract)
    {
        if (!OneMoreActionQueueProcessingBaseEngine::processSingleRecord(OneMoreActionQueue::findRecId(_contract.parmActionQueueRecId()), OneMoreActionQueueProcessingStage::Processing))
        {
            throw error("@OneMoreActionQueueFramework:OneMoreActionQueueProcessingFailed");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>preProcessActionQueueRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Pre-process single record from action queue
    /// </summary>
    /// <param name = "_contract"><c>OneMoreActionQueueProcessingSingleContract</c></param>
    [Hookable(false)]
    public void preProcessActionQueueRecord(OneMoreActionQueueProcessingSingleContract _contract)
    {
        if (!OneMoreActionQueueProcessingBaseEngine::processSingleRecord(OneMoreActionQueue::findRecId(_contract.parmActionQueueRecId()), OneMoreActionQueueProcessingStage::PreProcessing))
        {
            throw error("@OneMoreActionQueueFramework:OneMoreActionQueuePreProcessingFailed");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runBatchTaskBundle</Name>
				<Source><![CDATA[
    /// <summary>
    /// Run batch task bundle
    /// </summary>
    /// <param name = "_contract"><c>OneMoreActionQueueProcessingBatchTaskBundleContract</c></param>
    [Hookable(false)]
    public void runBatchTaskBundle(OneMoreActionQueueProcessingBatchTaskBundleContract _contract)
    {
        boolean throwError;
        
        ListEnumerator       le = List::create(_contract.parmRecIdListPacked()).getEnumerator();

        while (le.moveNext())
        {
            if (!OneMoreActionQueueProcessingBaseEngine::processSingleRecord(OneMoreActionQueue::findRecId(le.current()), OneMoreActionQueueProcessingStage::Processing))
            {
                throwError = true;
            }
        }

        //Batch job should fail if there were errors
        if (throwError)
        {
            throw Exception::Error;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>processActionQueueManual</Name>
				<Source><![CDATA[
    /// <summary>
    /// Process set of records from action queue
    /// </summary>
    /// <param name = "_contract"><c>OneMoreActionQueueProcessingManualContract</c></param>
    [Hookable(false)]
    public void processActionQueueManual(OneMoreActionQueueProcessingManualContract _contract)
    {
        SetEnumerator       se = _contract.parmRecordCollection().getEnumerator();
        
        batchBundleSize = 1;
        enableBatchBundling = this.isExecutingInBatch();
        runBatchTaskInSequence = true;

        while (se.moveNext())
        {
            this.executeAction(OneMoreActionQueue::findRecId(se.current()));
        }

        this.finalizeExecution();
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeAction</Name>
				<Source><![CDATA[
    private boolean executeAction(OneMoreActionQueue _oneMoreActionQueue)
    {
        boolean ret = true;
        
        if (enableBatchBundling && batchBundleSize && this.isExecutingInBatch())
        {
            this.addToQueue(_oneMoreActionQueue);
        }
        else
        {
            ret = OneMoreActionQueueProcessingBaseEngine::processSingleRecord(_oneMoreActionQueue, OneMoreActionQueueProcessingStage::Processing);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>finalizeExecution</Name>
				<Source><![CDATA[
    private void finalizeExecution()
    {
        if (enableBatchBundling && batchBundleSize && this.isExecutingInBatch())
        {
            if (batchBundlingList)
            {
                this.createTask(batchBundlingList.pack());
            }

            if (batchHeader)
            {
                batchHeader.save();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addTaskDependency</Name>
				<Source><![CDATA[
    private void addTaskDependency(SysOperationServiceController _currentTask)
    {
        if (runBatchTaskInSequence)
        {
            if (lastTask)
            {
                batchHeader.addDependency(_currentTask, lastTask, BatchDependencyStatus::FinishedOrError);
            }

            lastTask = _currentTask;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>