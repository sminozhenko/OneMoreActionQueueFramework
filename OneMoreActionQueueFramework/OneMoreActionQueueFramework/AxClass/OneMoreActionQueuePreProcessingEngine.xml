<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>OneMoreActionQueuePreProcessingEngine</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Pre-processing engine class
/// </summary>
internal class OneMoreActionQueuePreProcessingEngine extends OneMoreActionQueueProcessingBaseEngine
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static OneMoreActionQueuePreProcessingEngine construct()
    {
        return new OneMoreActionQueuePreProcessingEngine();
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectOneMoreActionQueueUpdReadPast</Name>
				<Source><![CDATA[
    protected boolean selectOneMoreActionQueueUpdReadPast()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkActionQueue</Name>
				<Source><![CDATA[
    protected boolean checkActionQueue()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processingStage</Name>
				<Source><![CDATA[
    protected OneMoreActionQueueProcessingStage processingStage()
    {
        return OneMoreActionQueueProcessingStage::PreProcessing;
    }

]]></Source>
			</Method>
			<Method>
				<Name>runInternal</Name>
				<Source><![CDATA[
    protected void runInternal()
    {
        newActionQueueStatus = OneMoreActionQueueStatus::Waiting;
        
        OneMoreActionQueueContract contract;

        try
        {
            contract = oneMoreActionQueueUpd.deserializedContract();
        }
        catch
        {
            error("@OneMoreActionQueueFramework:OneMoreActionQueueDeserializationFailed");
            newActionQueueStatus = OneMoreActionQueueStatus::Error;
        }
            
        if (newActionQueueStatus != OneMoreActionQueueStatus::Error)
        {
            Map providerMap = OneMoreActionQueuePreProcessingEngine::getProviders();

            MapEnumerator me = providerMap.getEnumerator();

            while (me.moveNext())
            {
                boolean failOnError;
            
                OneMoreActionQueueIPreProcessor preProcessor = me.currentValue();

                try
                {
                    if (preProcessor != null)
                    {
                        failOnError = preProcessor.failOnError();
                        preProcessor.run(oneMoreActionQueueUpd, contract);
                        
                    }
                    else
                    {
                        throw error("@OneMoreActionQueueFramework:OneMoreActionQueuePreProcessorIsNull");
                    }
                }
                catch
                {
                    if (failOnError)
                    {
                        newActionQueueStatus = OneMoreActionQueueStatus::Error;
                        break;
                    }
                }
            }
        }

        if (newActionQueueStatus == OneMoreActionQueueStatus::Waiting)
        {
            processedSuccessfully = true;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProviders</Name>
				<Source><![CDATA[
    private static Map getProviders()
    {
        SysPluginMetadataCollection metadataCollection = new SysPluginMetadataCollection();        
        
        Map ret = new Map(Types::Integer, Types::Class);
        Array providerList = SysPluginFactory::Instances(identifierStr(Dynamics.AX.Application), classStr(OneMoreActionQueueIPreProcessor), metadataCollection);

        int defaultProcessingOrder = 999;

        for (int counter = 1; counter <= providerList.lastIndex(); counter++)
        {
            int currentProcessingOrder;
            
            SysDictClass dc = new SysDictClass(classIdGet(providerList.value(counter)));

            OneMoreActionQueueProcessingOrderAttribute processingOrderAttribute = dc.getAttribute(classStr(OneMoreActionQueueProcessingOrderAttribute));

            if (processingOrderAttribute)
            {
                currentProcessingOrder = processingOrderAttribute.parmProcessingOrder();
            }
            else
            {
                currentProcessingOrder = defaultProcessingOrder;
                defaultProcessingOrder++;
            }

            currentProcessingOrder = currentProcessingOrder * 100; //Reserve 100 element in the map in case if there are multiples processing classes with the same order

            ret.add(currentProcessingOrder, providerList.value(counter));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected void postRun()
    {
        if (BatchHeader::isExecutingInBatch())
        {
            OneMoreActionQueueTypeTable oneMoreActionQueueTypeTable = oneMoreActionQueueCaller.oneMoreActionQueueTypeTable();

            OneMoreActionQueueProcessingType processingType = oneMoreActionQueueTypeTable.ProcessingType;

            if (processingType == OneMoreActionQueueProcessingType::Synchronous)
            {
                processingType = OneMoreActionQueueProcessingType::ReliableAsynchronous;
            }

            if (processingType == OneMoreActionQueueProcessingType::ReliableAsynchronous || processingType == OneMoreActionQueueProcessingType::Synchronous)
            {
                oneMoreActionQueueCaller.reread();
                OneMoreActionQueueProcessingHelper::processOneMoreActionQueue(oneMoreActionQueueTypeTable, oneMoreActionQueueCaller, processingType);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>