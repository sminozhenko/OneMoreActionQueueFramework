<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>OneMoreActionQueueProcessingQueryController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Action queue query processing controller
/// </summary>
internal abstract class OneMoreActionQueueProcessingQueryController extends SysOperationServiceController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();

        this.parmClassName(classStr(OneMoreActionQueueProcessingService));
        this.parmMethodName(methodStr(OneMoreActionQueueProcessingService, processQuery));
        this.parmExecutionMode(SysOperationExecutionMode::Synchronous);
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// Can be overridden to inidicate that the operation supports running in a different, non interactive
    /// session than the one from which it was launched
    /// from the client
    /// </summary>
    /// <returns>If true operation will be run synchrnously in an non GUI async session</returns>
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>beforeOperation</Name>
				<Source><![CDATA[
    protected void beforeOperation(SysOperationExecutionMode _executionMode)
    {
        OneMoreActionQueueProcessingQueryContract contract = this.getDataContractObject() as OneMoreActionQueueProcessingQueryContract;

        Query                   q = contract.getQuery();

        QueryBuildDataSource    qbdsActionQueue = q.dataSourceTable(tableNum(OneMoreActionQueue));
        QueryBuildDataSource    qbdsActionQueueTypeTable = qbdsActionQueue.addDataSource(tableNum(OneMoreActionQueueTypeTable));

        qbdsActionQueueTypeTable.relations(true);
        qbdsActionQueueTypeTable.joinMode(JoinMode::ExistsJoin);

        qbdsActionQueueTypeTable.addRange(fieldNum(OneMoreActionQueueTypeTable, Enabled)).value(queryValue(NoYes::Yes));
        qbdsActionQueueTypeTable.addRange(fieldNum(OneMoreActionQueueTypeTable, ProcessingType)).value(queryValue(this.processingType()));

        contract.setQuery(q);

        super(_executionMode);
    }

]]></Source>
			</Method>
			<Method>
				<Name>processingType</Name>
				<Source><![CDATA[
    /// <summary>
    /// <c>OneMoreActionQueueProcessingType</c>
    /// </summary>
    /// <returns><c>OneMoreActionQueueProcessingType</c></returns>
    abstract OneMoreActionQueueProcessingType processingType()
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>