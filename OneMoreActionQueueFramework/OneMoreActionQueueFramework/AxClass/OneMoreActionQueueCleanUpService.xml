<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>OneMoreActionQueueCleanUpService</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Service class for action queue clean-up operation
/// </summary>
internal class OneMoreActionQueueCleanUpService extends SysOperationServiceBase
{
    private     RecordInsertList                oneMoreActionQueueCleanUpTmpRIL;
    protected   OneMoreActionQueueCleanUpTmp    oneMoreActionQueueCleanUpTmp;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>cleanUp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clean-up action queue
    /// </summary>
    /// <param name = "_contract">An <c>OneMoreActionQueueCleanUpContract</c></param>
    [Hookable(false)]
    public void cleanUp(OneMoreActionQueueCleanUpContract _contract)
    {
        OneMoreActionQueueTypeTable     oneMoreActionQueueTypeTable;
        
        while select oneMoreActionQueueTypeTable
        {
            int                numberOfRecords;
            OneMoreActionQueue oneMoreActionQueue;
            int                totalDeletedRecords;
            
            oneMoreActionQueueTypeTable.setDefaultRetentionPeriod();

            this.initializeRecordInsertList();

            while select RecId
                from oneMoreActionQueue
                where oneMoreActionQueue.TypeId             == oneMoreActionQueueTypeTable.TypeId
                   && oneMoreActionQueue.CreatedDateTime    < DateTimeUtil::addDays(DateTimeUtil::utcNow(), -oneMoreActionQueueTypeTable.RetentionPeriod)
            {
                oneMoreActionQueueCleanUpTmp.RefRecId = oneMoreActionQueue.RecId;
                oneMoreActionQueueCleanUpTmpRIL.add(oneMoreActionQueueCleanUpTmp);

                numberOfRecords++;
                totalDeletedRecords++;

                if (numberOfRecords >= OneMoreActionQueueConstants::cleanUpBatchSize)
                {
                    numberOfRecords = 0;
                    this.deleteRecordsInternal();
                }
            }

            if (totalDeletedRecords)
            {
                this.deleteRecordsInternal();

                info(strFmt("@OneMoreActionQueueFramework:OneMoreActionQueueNumberOfDeletedRecord", totalDeletedRecords, oneMoreActionQueueTypeTable.TypeId));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeRecordInsertList</Name>
				<Source><![CDATA[
    private void initializeRecordInsertList()
    {
        oneMoreActionQueueCleanUpTmpRIL = new RecordInsertList(tableNum(OneMoreActionQueueCleanUpTmp), true, true, true, true, true, oneMoreActionQueueCleanUpTmp);
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteRecordsInternal</Name>
				<Source><![CDATA[
    private void deleteRecordsInternal()
    {
        oneMoreActionQueueCleanUpTmpRIL.insertDatabase();

        this.deleteRecords();

        delete_from oneMoreActionQueueCleanUpTmp;
        
        this.initializeRecordInsertList();
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteRecords</Name>
				<Source><![CDATA[
    /// <summary>
    /// Delete data from real tables
    /// </summary>
    protected void deleteRecords()
    {
        OneMoreActionQueue oneMoreActionQueue;

        OneMoreActionQueueCleanUpService::skipTableMethods(oneMoreActionQueue);

        delete_from oneMoreActionQueue
        exists join oneMoreActionQueueCleanUpTmp
            where oneMoreActionQueueCleanUpTmp.RefRecId == oneMoreActionQueue.RecId;
        
        OneMoreActionQueueLogTable oneMoreActionQueueLogTable;
        
        OneMoreActionQueueCleanUpService::skipTableMethods(oneMoreActionQueueLogTable);

        delete_from oneMoreActionQueueLogTable
        notexists join oneMoreActionQueue
            where oneMoreActionQueueLogTable.RefRecId == oneMoreActionQueue.RecId;

        OneMoreActionQueueDMFLogTable oneMoreActionQueueDMFLogTable;
        
        OneMoreActionQueueCleanUpService::skipTableMethods(oneMoreActionQueueDMFLogTable);

        delete_from oneMoreActionQueueDMFLogTable
        notexists join oneMoreActionQueue
            where oneMoreActionQueueDMFLogTable.RefRecId == oneMoreActionQueue.RecId;

        DocuRef docuRef;
        
        while select forupdate docuRef
            where docuRef.RefTableId    ==  tableNum(OneMoreActionQueue)
        notexists join oneMoreActionQueue
            where docuRef.RefRecId == oneMoreActionQueue.RecId
        {
            docuRef.delete();
        }

        OneMoreActionQueueBatchJobCorrelation oneMoreActionQueueBatchJobCorrelation;

        OneMoreActionQueueCleanUpService::skipTableMethods(oneMoreActionQueueBatchJobCorrelation);

        delete_from oneMoreActionQueueBatchJobCorrelation
        notexists join oneMoreActionQueue
            where oneMoreActionQueueBatchJobCorrelation.ActionQueueRecId == oneMoreActionQueue.RecId;

        OneMoreActionQueueDocumentCorrelation oneMoreActionQueueDocumentCorrelation;

        OneMoreActionQueueCleanUpService::skipTableMethods(oneMoreActionQueueDocumentCorrelation);

        delete_from oneMoreActionQueueDocumentCorrelation
        notexists join oneMoreActionQueue
            where oneMoreActionQueueDocumentCorrelation.ActionQueueRecId == oneMoreActionQueue.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>skipTableMethods</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal static void skipTableMethods(Common _common)
    {
        _common.skipDeleteActions(true);
        _common.skipDataMethods(true);
        _common.skipAosValidation(true);
        _common.skipDatabaseLog(true);
        _common.skipEvents(true);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>